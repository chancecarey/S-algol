program = [sequence];

sequence =
  declaration [";" sequence]
| clause [";" sequence] ;

declaration = 
  let_decl
| structure_decl
| proc_decl
| forward ;

let_decl =
  "let" identifier init_op clause ;

init_op =
  "="
| ":=" ;

structure_decl =
  "structure" identifier ["(" [field_list] ")"] ;

field_list =
  type1 identifier_list [";" field_list] ;

proc_decl =
  "procedure" identifier ["(" [parameter_list] [arrow type_id] ")"] ";" clause ;

parameter_list =
  parameter [";" parameter_list] ;

parameter =
  type1 identifier_list
| structure_decl
| proc_type identifier_list ;

proc_type =
  "(" [ptype_list] [arrow type_id] ")" ;

ptype_list =
  type1 ["," ptype_list]
| proc_type ["," ptype_list]
| s_decl ["," ptype_list] ;

s_decl =
  "structure" "(" type1 ["," type1]* ")" ;

forward =
  "forward" identifier proc_type ;

type1 =
  ["c"] type_id ;

type_id = 
  "int"
| "real"
| "bool"
| "string"
| "pixel"
| "pic"
| "pntr"
| "file"
| "#pixel"
| "#cpixel"
| star type1 ;

identifier_list =
  identifier ["," identifier_list] ;

clause =
  "if" clause if_clause_then
| "repeat" clause "while" clause ["do" clause]
| "while" clause "do" clause
| "for" identifier "=" clause "to" clause ["by" clause] "do" clause
| "case" clause "of" case_list "default" ":" clause
| "abort"
| name ":=" clause
| write_clause
| raster
| expression ;

if_clause_then =
  "do" clause
| "then" clause "else" clause ;

case_list =
  clause_list ":" clause ";" [case_list] ;

write_clause =
  "write" write_list
| "output" clause "," write_list
| "out.byte" clause "," clause "," clause
| "out.16" clause "," clause "," clause
| "out.32" clause "," clause ;

write_list =
  clause [":" clause] ["," write_list] ;

raster =
  raster_op clause "onto" clause ;

raster_op =
  "ror"
| "rand"
| "xor"
| "copy"
| "nand"
| "nor"
| "not"
| "xnor" ;

clause_list =
  clause ["," clause_list] ;
  
expression =
  exp1 ["or" exp1]* ;

exp1 =
 exp2 ["and" exp2]* ;

exp2 =
  ["~"] exp3 [rel_op exp3] ;

exp3 =
  exp4 [add_op exp4] ;

exp4 =
  exp5 [mult_op exp5] ;

exp5 =
  [add_op] exp6 ;

exp6 =
  standard_exp
| literal
| value_constructor
| "(" clause ")"
| "begin" [sequence] "end"
| "{" [sequence] "}"
| expression "(" clause bar clause ")"
| expression "(" dereference ")"
| application
| structure_creation
| name
| bounds_op "(" clause ")" ;

dereference =
  clause_list ;

application =
  identifier ["(" [clause_list] ")"] ;

structure_creation =
  identifier ["(" [clause_list] ")"] ;

name =
  identifier
| expression "(" clause_list ")" ["(" clause_list ")"]* ;

bounds_op =
  "upb"
| "lwb" ;

value_constructor =
  vector_constr
| image_constr
| subimage_constr
| picture_constr ;

vector_constr =
  "vector" range "of" clause
| "@" clause "of" type1 lsb clause ["," clause]* rsb ;

range =
  clause "::" clause ["," range"] ;

image_constr =
  "image" clause "by" clause "of" clause ;

subimage_constr =
  "limit" clause ["to" clause "by" clause] ["at" clause "," clause] ;

picture_constr =
  "shift" clause "by" clause "," clause
| "scale" clause "by" clause "," clause
| "rotate" clause "by" clause
| "colour" clause "in" clause
| "text" clause "from" clause "," clause "to" clause "," clause
| lsb clause "," clause rsb ;

literal =
  integer_literal
| real_literal
| boolean_literal
| string_literal
| pixel_literal
| pntr_literal
| file_literal ;

integer_literal =
  [add_op] int_literal ;

int_literal =
  digit [digit]* ;

real_literal =
  integer_literal "." [int_literal] ["e" integer_literal] ;

boolean_literal =
  "true"
| "false" ;

string_literal =
  double_quote [char]* double_quote ;

char =
  char_except_quote
| special_character ;

special_character =
  single_quote special_follow ;

special_follow =
  "n"
| "p"
| "o"
| "t"
| "b"
| single_quote
| double_quote ;

pixel_literal =
  "on" ["&" pixel_literal]
| "off" ["&" pixel_literal] ;

pntr_literal =
  "nil" ;

file_literal =
  "nullfile" ;

lab = 
  "<" ;

rab =
  ">" ;

lsb = 
  "[" ;

rsb = 
  "]" ;

star =
  "*" ;

bar =
  "|" ;

add_op = 
  "+"
| "-" ;

mult_op =
  "++"
| int_mult_op
| real_mult_op
| pic_op
| pixel_op;

int_mult_op =
  "div"
| "rem"
| star ;

real_mult_op =
  "/"
| star ;

pic_op =
  "^"
| "&" ;

pixel_op =
  "&" ;

rel_op =
  eq_op
| compar_op
| type_op ;

eq_op =
  "="
| "!=" ;

compar_op = 
  "<"
| "<="
| ">"
| ">=" ;

type_op =
  "is"
| "isnt" ;

arrow =
  "->" ;

double_quote =
  "\"" ;

single_quote = 
  "'" ;

identifier =
  id
| standard_id ;

id =
  letter [id_follow] ;

id_follow =
  letter [id_follow]
| digit [id_follow]
| "." id_follow ;

standard_exp =
  standard_name ["(" clause ")"] ;

standard_name =
  "upb"
| "lwb"
| "eof"
| "read.a.line"
| "read"
| "readi"
| "readr"
| "readb"
| "peek"
| "reads"
| "read.name"
| "read.byte"
| "read.16"
| "read.32" ;

standard_id =
  "r.w"
| "i.w"
| "s.w"
| "s.o"
| "s.i"
| "maxint"
| "maxreal"
| "epsilon"
| "pi"
| "cursor"
| "screen" ;

*letter
*digit
*char_except_quote